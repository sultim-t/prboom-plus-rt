dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.5 2000/05/07 06:48:48 jessh Exp $
AC_INIT(src/d_main.c)

dnl --- Check for system type
AC_CANONICAL_SYSTEM

use_i386_asm=no
case "$target" in
        i?86-*) AC_DEFINE(I386) use_i386_asm=yes ;;
esac

echo "checking whether can use x86 asm versions of some functions..." $use_i386_asm
AM_CONDITIONAL(I386_ASM, test "$use_i386_asm" = yes)

CFLAGS_OPT="-O2 -fomit-frame-pointer"
dnl --- Option to enable debugging
AC_ARG_ENABLE(debug,[  --enable-debug	  turns on various debugging features, like range checking and internal heap diagnostics],
AC_DEFINE(RANGECHECK)
AC_DEFINE(INSTRUMENTED)
AC_DEFINE(TIMEDIAG)
CFLAGS_OPT="-g"
)
dnl --- some defaults for CFLAGS : josh
if test -z "$CFLAGS" ; then 
	CFLAGS="$CFLAGS_OPT -Wall -Winline -Wcast-align -Wwrite-strings -ffast-math"
fi
export CFLAGS

dnl --- Fire up automake
AM_INIT_AUTOMAKE(lxdoom, 1.4.5)
AM_CONFIG_HEADER(config.h)

dnl --- Checks for programs.
AC_PROG_CC

dnl --- Try for processor optimisations
AC_ARG_ENABLE(cpu-opt,[  --disable-cpu-opt	  turns off cpu specific optimisations],,[
case "$target" in
	# marginal gains from aligning code
i386-*) SYS_CFLAGS="-m386" ;;
i486-*) SYS_CFLAGS="-m486" ;;
	# nothing special for pentium
	# CMOV op on ppro/II/686 can help us
i686-*) SYS_CFLAGS="-mcpu=i686 -march=i686" ;;
esac

echo -n "checking whether $CC supports CPU optimisations $SYS_CFLAGS..."
echo 'void f(){}' > conftest.c
if test -z "`${CC-cc} $SYS_CFLAGS -c conftest.c 2>&1`"; then
	CFLAGS="$CFLAGS $SYS_CFLAGS"
	echo "yes"
else
	echo "no"
fi
])

dnl --- Checks for typedefs
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_UID_T

AC_DECL_SYS_SIGLIST

dnl --- Compiler characteristics
AC_C_CONST
AC_C_INLINE
if test "$cross_compiling" != "yes"; then
 AC_C_BIGENDIAN
fi

dnl --- Libraries
AC_PATH_X
AC_PATH_XTRA
if test "x$x_includes" != x; then
  CFLAGS="$CFLAGS -I$x_includes"
else
  CFLAGS="$CFLAGS"
fi
echo "CFLAGS complete: $CFLAGS"
LIBS="$LIBS $X_LIBS $X_EXTRA_LIBS"
dnl --- cphipps - no longer need sqrt(3) AC_CHECK_LIB(m,sqrt)
AC_CHECK_LIB(Xext,XShmCreateImage,AC_DEFINE(HAVE_LIBXEXT) LIB_XEXT=-lXext,LIB_XEXT=,-lXext -lX11)
dnl --- CPhipps - disable XFree86 DGA support for release
dnl AC_CHECK_LIB(Xxf86dga,XF86DGADirectVideo,AC_DEFINE(HAVE_LIBXXF86DGA) LIB_XDGA=-lXxf86dga,LIB_XDGA=,-lXxf86dga -lXext -lX11)
AC_CHECK_LIB(vga,vga_setpalvec,BUILD_LSDOOM=lsdoom,BUILD_LSDOOM=)
AC_CHECK_LIB(ossaudio,_oss_ioctl,LIB_SND=-lossaudio,LIB_SND=)


AM_PATH_SDL(1.0.1, BUILD_SDLDOOM=lsdldoom, BUILD_SDLDOOM=)

AC_CHECK_LIB(SDL_mixer,Mix_LoadMUS,[
    AC_DEFINE(HAVE_LIBSDL_MIXER)
    MIXER_LIBS=-lSDL_mixer
],,$SDL_LIBS)

AC_CHECK_FUNC(socket,[
     has_net=yes
     BUILD_SERVER=lxdoom-game-server
],)
AC_ARG_ENABLE(net-game,[  --disable-net-game      turns off network game
support],[
     has_net=no
     BUILD_SERVER=
],) 
AM_CONDITIONAL(HAVE_NET, test "$has_net" = yes)
if test "$has_net" = yes
then
 AC_DEFINE(HAVE_NET)
fi

dnl --- Library functions
AC_CHECK_FUNC(stricmp,,AC_DEFINE(stricmp,strcasecmp)) 
AC_CHECK_FUNC(strnicmp,,AC_DEFINE(strnicmp,strncasecmp)) 
AC_CHECK_FUNCS(inet_aton inet_pton inet_ntop setsockopt vsnprintf snprintf)

dnl --- Check for headers
dnl - First, joystick
AC_CHECK_HEADERS(linux/joystick.h)
dnl - want ffz()
AC_CHECK_HEADERS(linux/bitops.h)
dnl - fast byte swaping code
AC_CHECK_HEADERS(asm/byteorder.h)
dnl - more standard stuff
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(unistd.h)
dnl - Check for soundcard.h
BUILD_SNDSERV=
AC_CHECK_HEADERS(sys/soundcard.h,BUILD_SNDSERV=sndserv)
AC_CHECK_HEADERS(machine/soundcard.h,BUILD_SNDSERV=sndserv)
AC_CHECK_HEADERS(soundcard.h,BUILD_SNDSERV=sndserv)

dnl --- Check for IPv6 stuff
AC_EGREP_HEADER(sockaddr_in6,netinet/in.h,AC_DEFINE(HAVE_IPv6))

dnl --- Always use highres mode, unless explicitely --disabled
AC_ARG_ENABLE(highres,[  --disable-highres	  disables support for resulutions greater than 320x200],,AC_DEFINE(HIGHRES))
dnl --- By default, simple checks with little overhead are done to catch memory corruption, or
dnl --- problems with new code
AC_ARG_ENABLE(checks,[  --disable-checks	  disables some basic internal sanity checks],,
AC_DEFINE(SIMPLECHECKS)
AC_DEFINE(ZONEIDCHECK))
AC_ARG_ENABLE(heapcheck,[  --enable-heapcheck	  turns on continuous heap checking (very slow)],AC_DEFINE(CHECKHEAP))
AC_ARG_ENABLE(heapdump,[  --enable-heapdump	  turns on dumping the heap state for debugging],AC_DEFINE(HEAPDUMP))

dnl --- Decide what programs to build
if test "$no_x" != "yes"
then BUILD_LXDOOM=lxdoom
else
BUILD_LXDOOM=
fi

AC_SUBST(BUILD_SNDSERV)
AC_SUBST(BUILD_LXDOOM)
AC_SUBST(BUILD_LSDOOM)
AC_SUBST(BUILD_SDLDOOM)
AC_SUBST(BUILD_SERVER)
AC_SUBST(MIXER_CFLAGS)
AC_SUBST(MIXER_LIBS)
AC_SUBST(NET_CFLAGS)
AC_SUBST(NET_LIBS)
AC_SUBST(LIB_XEXT)
AC_SUBST(LIB_XDGA)
AC_SUBST(LIB_SND)

dnl --- Pass install dirs to C pre-processor
dnl --- YUCK! but I can't see a better way to do this
if test "$prefix" = "NONE"
then prefix="/usr/local"
fi
if test "$exec_prefix" = "NONE"
then exec_prefix=$prefix
fi

dnl --- decide directories
gamesdir=$prefix/games
exp_gamesdir=`eval "echo $gamesdir"`
exp_datadir=`eval "echo $datadir"`
waddir=$exp_datadir/games/doom
dnl --- now finally spit them out
AC_DEFINE_UNQUOTED(SNDSERV_PATH,"$exp_gamesdir/sndserv")
AC_DEFINE_UNQUOTED(MUSSERV_PATH,"$exp_gamesdir/musserv")
AC_DEFINE_UNQUOTED(DOOMWADDIR,"$waddir")

dnl --- output
AC_OUTPUT(Makefile src/Makefile doc/Makefile data/Makefile lxdoom.spec)

dnl
dnl $Log: configure.in,v $
dnl Revision 1.5  2000/05/07 06:48:48  jessh
dnl Set default endianness to little when cross compiling and use x86 asm on all
dnl systems suporting it
dnl
dnl Revision 1.4  2000/05/06 03:54:47  jessh
dnl Use fast assembly byte swaping macros on Linux
dnl
dnl Revision 1.3  2000/05/05 13:51:49  cph
dnl Get SDL configuration and compilation working on POSIX
dnl
dnl Revision 1.2  2000/05/05 08:28:50  cph
dnl Include config.h, unistd.h where needed for POSIX systems
dnl Remove in-file CVS logs, they're getting big
dnl
dnl Revision 1.1.1.1  2000/05/04 07:54:22  proff_fs
dnl initial login on sourceforge as prboom2
dnl
dnl Revision 1.23  2000/04/29 16:18:52  cph
dnl *** empty log message ***
dnl
dnl Revision 1.22  2000/04/09 13:39:43  cph
dnl Get ./configure heap dumping option working
dnl Fix w_wad.c check
dnl
dnl Revision 1.21  2000/04/05 11:14:52  cph
dnl Fix typo
dnl
dnl Revision 1.20  2000/04/05 10:47:27  cph
dnl Remove dead #ifdef magic, rely on config.h now
dnl Make sndserv work on (Open|Net)BSD, using libossaudio
dnl Make --enable-debug compile with -g
dnl Make asm stuff only compile on Linux and FreeBSD
dnl (draw(col|span).s failed on OpenBSD, linker troubles)
dnl
dnl Revision 1.19  2000/04/03 21:47:35  cph
dnl Better detection fo IPv6
dnl Minor header file corrections
dnl
dnl Revision 1.18  2000/04/03 17:05:51  cph
dnl Split client specific stuff from l_udp.c to new l_network.c
dnl Move server specific stuff from l_udp.c to d_server.c
dnl Update copyright notices
dnl Restructure ready for IPv6 support
dnl Use fcntl instead of ioctl to set socket non-blocking
dnl
dnl Revision 1.17  2000/03/29 10:30:45  cph
dnl Bump up version number
dnl
dnl Revision 1.16  2000/03/27 10:33:22  cph
dnl Bump version number
dnl
dnl Revision 1.15  2000/03/17 00:12:02  cph
dnl Bump version number
dnl Move SYS_CFLAGS stuff after compiler detection
dnl Add option to not set CPU optimisation flags (for RPMs)
dnl Test CPU optimisation flags (so gcc 2.7.2 works)
dnl
dnl Revision 1.14  2000/03/16 20:14:08  cph
dnl Add docs to make install, add RPM building
dnl
dnl Revision 1.13  2000/02/26 19:22:53  cph
dnl Add pid_t test for src/d_client.c, uid_t for l_video_svgalib.c, and sys/wait.h for src/d_client.c
dnl
dnl Revision 1.12  2000/02/19 23:49:09  cphipps
dnl Fix build problems on systems with X headers in include path by default
dnl
dnl Revision 1.11  2000/01/26 08:44:24  cphipps
dnl Support filio.h so Solaris can get the non-blocking socket ioctl
dnl
dnl Revision 1.10  2000/01/25 21:33:22  cphipps
dnl Fix security in case of being setuid
dnl
dnl Revision 1.9  1999/12/18 22:25:10  cphipps
dnl Remove -pipe from the CFLAGS, as this is only guaranteed to work with GNU as(1)
dnl
dnl Revision 1.8  1999/12/18 15:48:06  cphipps
dnl Use autoconf script to detect inet_aton
dnl
dnl Revision 1.7  1999/11/04 13:02:20  cphipps
dnl Remove check/use of libm
dnl
dnl Revision 1.6  1999/11/01 00:16:09  cphipps
dnl Added checks for inline and sys_siglist
dnl Update version number to 1.4.2
dnl
dnl Revision 1.5  1999/10/27 07:43:58  cphipps
dnl Added stuff to set up CFLAGS right for different architectures,
dnl thanks to patch from Josh Parsons.
dnl
dnl Revision 1.4  1999/10/18 19:16:42  cphipps
dnl Version 1.4.1
dnl
dnl Revision 1.3  1999/10/12 15:42:50  cphipps
dnl Changed version to 1.4.0
dnl
dnl Revision 1.2  1999/10/02 11:43:37  cphipps
dnl Added autoconf options to control diagnostics
dnl
dnl Revision 1.1  1999/09/10 19:52:44  cphipps
dnl Initial revision
dnl
dnl
